\import Utils
\import hw02 ()
\import lect03 (M5, fileTest)

-- 1. Докажите ассоциативность && и ||.

\func &&-assoc (x y z : Bool) : (x && y) && z = x && (y && z) \elim x
  | true => idp
  | false => idp

\func ||-assoc (x y z : Bool) : (x || y) || z = x || (y || z) \elim x
  | true => idp
  | false => idp

-- 2. Докажите, что fac 3 равно 6.

\func fac3 : hw02.fac 3 = 6 => idp

-- 3. Докажите следующие пропозициональные тавтологии.

-- t1 : (P and Q -> R) -> P -> Q -> R
\func t1 {P Q R : \Type} (f : \Sigma P Q -> R) (p : P) (q : Q) : R => f (p, q)

-- t2 : (P -> R) -> (Q -> R) -> P or Q -> R
\func t2 {P Q R : \Type} (f : P -> R) (g : Q -> R) (h : Either P Q) : R \elim h
  | inl p => f p
  | inr q => g q

-- t3 : ((P -> Q -> R) -> P) -> (P -> R) -> R
\func t3 {P Q R : \Type} (f : (P -> Q -> R) -> P) (g : P -> R) : R => g (f (\lam p _ => g p))

-- 4. Сформулируйте и докажите следующие пропозициональные тавтологии.

-- t4 : (P -> Q -> R) -> P and Q -> R

\func t4 {P Q R : \Type} (f : P -> Q -> R) (g : \Sigma P Q) : R \elim g
  | (p,q) => f p q

-- t5 : (P or Q -> P and Q) -> (P -> Q) and (Q -> P)

\func t5 {P Q : \Type} (f : Either P Q -> \Sigma P Q) : \Sigma (P -> Q) (Q -> P) => (g f, h f) \where {
  \func g {P Q : \Type} (f : Either P Q -> \Sigma P Q) (p : P) : Q => (f (inl p)).2
  \func h {P Q : \Type} (f : Either P Q -> \Sigma P Q) (q : Q) : P => (f (inr q)).1
}

-- t6 : ((((P -> Q) -> P) -> P) -> Q) -> Q

\func t6 {P Q : \Type} (f : (((P -> Q) -> P) -> P) -> Q) : Q => f (
  \lam (g : (P -> Q) -> P) => g (
    \lam (h : P) => f (
      \lam (f' : (P -> Q) -> P) => h
    )
  )
)

-- 5. Докажите следующие утверждения.

-- s1 : Если для любого x : Nat верно P(x), то существует x : Nat для которого P(x) верно.
\func s1 (P : Nat -> \Type) (h : \Pi (x : Nat) -> P x) : \Sigma (x : Nat) (P x) => (0, h 0)

-- s2 : Если не существует x : Nat такого, что верно P(x), то P(3) не верно.
\func s2 (P : Nat -> \Type) (h : \Sigma (x : Nat) (P x) -> Empty) : P 3 -> Empty => (\lam x => h (3, x))

-- 6. Сформулируйте и докажите следующие утверждения.

-- s3 : Если для всех x : Nat верно, что P(x) влечет Q(x), и существует x : Nat для которого верно P(x), то существует x : Nat для которого верно Q(x).

\func s3 (P Q : Nat -> \Type) (f : \Pi (x : Nat) -> (P x -> Q x)) (g : \Sigma (x : Nat) (P x)) : \Sigma (x : Nat) (Q x) =>
  (g.1, f g.1 g.2)

-- s4 : Если для всех x : Nat либо не верно P(x), либо не верно Q(x), то P(3) влечет, что Q(3) не верно.

\func s4 (P Q : Nat -> \Type) (f : \Pi (x : Nat) -> (Either (P x -> Empty) (Q x -> Empty))) : (P 3 -> (Q 3 -> Empty)) =>
  \lam (x : P 3) => \case (f 3) \with {
    | inl pp => absurd (pp x)
    | inr qq => qq
  }

-- 7. Чему равно fileTest (в нормальной форме)? Докажите это.

\func fileTestTest : fileTest = (17, 8, 13, 55, 18, 21) => idp

-- 8. Чему равно M5.t''' (в нормальной форме)? Докажите это.

\func t'''Test : M5.t''' = ((82, 42, 61, 82, 34, 42, 61, 29), 25, (42, 29)) => idp

-- 9. Докажите ассоциативность ++.

\func ++-assoc {A : \Type} (xs ys zs : List A) : (xs ++ ys) ++ zs = xs ++ (ys ++ zs) \elim xs
  | nil => idp
  | cons a l => pmap (cons a) (++-assoc l ys zs)